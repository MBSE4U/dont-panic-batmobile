{
 "cells": [
  {
   "cell_type": "markdown",
   "id": "63571d9c",
   "metadata": {},
   "source": [
    "# Example Batmobile from the book Don't Panic - The Absolute Beginners Guide to SysML v2"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "d144dff3",
   "metadata": {},
   "source": [
    "The book \"Don't Panic - The Absolute Beginners Guide to SysML v2\" by Tim Weilkiens and Christian Muggeo uses the Batmobile as a fictional example. The Batmobile example in this book was inspired by the legendary lecture Jon Holt gave dressed in a Batman costume.\n",
    "\n",
    "The book provides an overview of SysML v2, so it is a small example with many snippets shown throughout the book. There is therefore also no sophisticated package structure. All elements are in one package."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 5,
   "id": "33460eca",
   "metadata": {
    "tags": [],
    "vscode": {
     "languageId": "sysml"
    }
   },
   "outputs": [
    {
     "data": {
      "text/plain": [
       "Package Dont_Panic_Batmobile (d83ea999-1eae-48ef-8e7c-d32cddfcd22c)\n"
      ]
     },
     "execution_count": 5,
     "metadata": {},
     "output_type": "execute_result"
    }
   ],
   "source": [
    "package Dont_Panic_Batmobile {\n",
    "    doc /* The book \"Don't Panic - The Absolute Beginners Guide to SysML v2\" by Tim Weilkiens and Christian Muggeo \n",
    "         * uses the Batmobile as a fictional example. \n",
    "         * \n",
    "         * The Batmobile example in this book was inspired by the legendary lecture Jon Holt gave dressed in a Batman costume.\n",
    "         *\n",
    "         * The book provides an overview of SysML v2, so it is a small example with many snippets shown throughout the book. \n",
    "         * There is therefore also no sophisticated package structure. All elements are in one package.\n",
    "         *\n",
    "         * Copyright (c) 2023 Tim Weilkiens, Christian Muggeo\n",
    "         *\n",
    "         *  Licensed under the Apache License, Version 2.0 (the \"License\");\n",
    "         *  you may not use this file except in compliance with the License.\n",
    "         *  You may obtain a copy of the License at\n",
    "         *\n",
    "         *  http://www.apache.org/licenses/LICENSE-2.0\n",
    "         *\n",
    "         *  Unless required by applicable law or agreed to in writing, software\n",
    "         *  distributed under the License is distributed on an \"AS IS\" BASIS,\n",
    "         *  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.\n",
    "         *  See the License for the specific language governing permissions and\n",
    "         *  limitations under the License.\n",
    "         */\n",
    "        \n",
    "    //\n",
    "    // Common Definitions\n",
    "    //\n",
    "    \n",
    "    // Common vehicle definition\n",
    "    part def Vehicle {\n",
    "        item driver;\n",
    "        part engine;\n",
    "        attribute actualSpeed :> ISQ::speed;\n",
    "    }\n",
    "    \n",
    "    // Interface for a power connection\n",
    "    interface def PowerInterface {\n",
    "        end supplierPort : PowerIP;\n",
    "        end consumerPort : ~PowerIP;\n",
    "        flow of Power from supplierPort.power to consumerPort.power;\n",
    "    }\n",
    "    // Interaction point for power\n",
    "    port def PowerIP {\n",
    "        out item power : Power;\n",
    "    }\n",
    "    // Definition of power\n",
    "    item def Power {\n",
    "        attribute value;\n",
    "    }\n",
    "    \n",
    "    // Definition of a wheel with a bounding box\n",
    "    part def Wheel {\n",
    "        item boundingBox : ShapeItems::Box [1] :> boundingShapes {\n",
    "            attribute :>> length :> ISQ::length = 80 [SI::cm];\n",
    "            attribute :>> width :> ISQ::length  = 26 [SI::cm];\n",
    "            attribute :>> height :> ISQ::length = 80 [SI::cm];\n",
    "        }\n",
    "        attribute pressure;\n",
    "    }\n",
    "    \n",
    "    /// Definition of an engine for the Batmobile\n",
    "    part def BatmobileEngine {\n",
    "        perform action rocketBoost {\n",
    "            in cmd : EngineCommand = engineControl.cmd;\n",
    "        }\n",
    "        port engineControl {\n",
    "            in item cmd : EngineCommand;\n",
    "        }\n",
    "    }\n",
    "    \n",
    "    // Definition of commands to control the engine\n",
    "    item def EngineCommand;\n",
    "\n",
    "    \n",
    "    //\n",
    "    // The Batmobile\n",
    "    //\n",
    "    #system part def Batmobile :> Vehicle {\n",
    "        :>> problemStatement := \"As a Hero, Batman needs a cool vehicle.\";\n",
    "        :>> systemIdea := \"The Batmobile is a black vehicle...\";      \n",
    "        \n",
    "        part seat[2];\n",
    "        part body;\n",
    "        part wheels[4] : Wheel;\n",
    "        part frontLeftWheel :> wheels;\n",
    "        part frontRightWheel :> wheels;\n",
    "        part rearLeftWheel :> wheels;\n",
    "        part rearRightWheel :> wheels;\n",
    "\n",
    "        part battery {\n",
    "            port powerPort : PowerIP;\n",
    "            attribute capacity;\n",
    "        }\n",
    "        part batmobileEngine : BatmobileEngine :>> engine {\n",
    "            port enginePort : ~PowerIP;\n",
    "        }        \n",
    "        interface bat2eng : PowerInterface connect \n",
    "            battery.powerPort to batmobileEngine.enginePort;        \n",
    "    }\n",
    "\n",
    "    // A special version: the Batmobile Next Generation\n",
    "    part def BatmobileNG :> Batmobile {\n",
    "        part bulletProofWheels[4] :>> wheels {\n",
    "            part metalCoating;\n",
    "        }\n",
    "    }\n",
    "    \n",
    "\n",
    "    //\n",
    "    // Batman\n",
    "    //\n",
    "    individual item def Batman :> Hero {\n",
    "        attribute realName = \"Jon Holt\";\n",
    "    }   \n",
    "    item def Hero;\n",
    "    \n",
    "    \n",
    "    //\n",
    "    // Timeslices\n",
    "    //\n",
    "    \n",
    "    // Since we introduced the language extension \"system\" and used it for the definition of Batmobile, the part must also use the extension.\n",
    "    // In the book, \"bm1\" is just a part, and Batmobile a part definition except in the paragraph where we explain the language extension.\n",
    "    #system part bm1 : Batmobile {\n",
    "        timeslice batmanDriving {\n",
    "            item :>> driver : Batman;\n",
    "            snapshot :>> start {attribute :>> battery.capacity = 40000 [SI::'watt hour'];}\n",
    "            snapshot :>> done {attribute :>> battery.capacity = 42 [SI::'watt hour'];}\n",
    "        }\n",
    "         then timeslice charging {\n",
    "            item :>> driver = null;\n",
    "         }\n",
    "    }\n",
    "      \n",
    "    //\n",
    "    // Behavior modeling\n",
    "    //\n",
    "    action def 'Drive Batmobile' {\n",
    "        first start;\n",
    "        then action startBatmobile;\n",
    "        then action scanEnvironment {out status : StatusKind;}\n",
    "        then decide;\n",
    "            if scanEnvironment.status == StatusKind::safe then 'Switch to standard mode';\n",
    "            if scanEnvironment.status == StatusKind::alert then 'Switch to alert mode';\n",
    "        action 'Switch to standard mode';\n",
    "        then endOfStatusCheck;\n",
    "        action 'Switch to alert mode';\n",
    "        then endOfStatusCheck ;\n",
    "        merge endOfStatusCheck ;\n",
    "        then done;\n",
    "    }\n",
    "    enum def StatusKind { enum safe; enum alert;}\n",
    "\n",
    "    \n",
    "    //\n",
    "    // Use Cases\n",
    "    //\n",
    "    use case def 'Activate rocket booster' {\n",
    "        subject bm : Batmobile;\n",
    "        actor driver : Batman;\n",
    "        objective {\n",
    "            doc /*\n",
    "                 * The driver wants to activate \n",
    "                 * the rocket booster to increase \n",
    "                 * the speed extremely.\n",
    "               */\n",
    "        }\n",
    "    }\n",
    "    action def ActivateRocketBooster :> 'Activate rocket booster' {\n",
    "        first start;\n",
    "        then action prepareBoost;\n",
    "        then action activateBoost;\n",
    "        then done;\n",
    "    }\n",
    "    \n",
    "    //\n",
    "    // Requirements\n",
    "    //\n",
    "    \n",
    "    requirement def VehicleMaxSpeed {\n",
    "        doc /* The actual speed of the vehicle shall \n",
    "             * be less or equal than the maximum speed.\n",
    "             */\n",
    "        subject vehicle : Vehicle;\n",
    "        stakeholder pm : ProductManagement;        \n",
    "        attribute maxSpeed :> ISQ::speed;\n",
    "        require constraint \n",
    "            {vehicle.actualSpeed <= maxSpeed}\n",
    "    }\n",
    "    item def ProductManagement;\n",
    "    \n",
    "    requirement batmobileSpecification {    \n",
    "        requirement <'REQ42'> \n",
    "            batmobileMaxSpeed : VehicleMaxSpeed {\n",
    "                attribute :>> maxSpeed = 230 [SI::'km/h'];\n",
    "        }\n",
    "        requirement <'REQ43'> \n",
    "            batmobileAcceleration;\n",
    "    }\n",
    "    \n",
    "    #system batmobileDesignV23 : Batmobile;\n",
    "    satisfy batmobileSpecification by batmobileDesignV23;\n",
    "    \n",
    "    \n",
    "    //\n",
    "    // Language Extension\n",
    "    //\n",
    "    part def System {\n",
    "        doc    /* Represents an engineered system that is a system designed or adapted \n",
    "                * to interact with an anticipated operational environment to achieve one or more \n",
    "                * intended purposes while complying with applicable constraints.  \n",
    "                */\n",
    "        attribute problemStatement : ScalarValues::String\n",
    "             { doc /* The problem solved by the system. */}\n",
    "        attribute systemIdea : ScalarValues::String\n",
    "             { doc /* The elevator pitch of how the system solves the problem. */}\n",
    "    }\n",
    "    part systems[*] : System;\n",
    "    metadata def <system> SystemDSM :> Metaobjects::SemanticMetadata {\n",
    "        :>> baseType = systems meta SysML::Usage;\n",
    "    }\n",
    "\n",
    "    \n",
    "    //\n",
    "    // Viewpoint and View\n",
    "    //\n",
    "    part def HeroAssociation;\n",
    "        concern 'Reduce the number of special parts' {\n",
    "            doc /*\n",
    "                * Reduce the number of special parts to reduce the dependency\n",
    "                * to special suppliers and experts.\n",
    "                */\n",
    "        stakeholder heroAss : HeroAssociation;\n",
    "    }\n",
    "\n",
    "    viewpoint 'system components' {\n",
    "        frame 'Reduce the number of special parts';\n",
    "        require constraint {\n",
    "            doc /*\n",
    "                * A system components view shall show the hierarchical\n",
    "                * part decomposition of a system, starting with a\n",
    "                * specified root part.\n",
    "                */\n",
    "        }\n",
    "    }\n",
    "    \n",
    "    view def 'Part list' {\n",
    "        satisfy 'system components';\n",
    "        filter @SysML::PartUsage;\n",
    "    }\n",
    "\n",
    "    view batmobileParts : 'Part list' {\n",
    "        expose Dont_Panic_Batmobile::**;\n",
    "        render Views::asElementTable;\n",
    "    }  \n",
    "    \n",
    "    //\n",
    "    // Variability Modeling\n",
    "    //\n",
    "    \n",
    "    // Shared Assets Library\n",
    "    part def StandardEngine :> BatmobileEngine;\n",
    "    part def XtremeEngine :> BatmobileEngine;\n",
    "\n",
    "    part def StandardWheel :> Wheel;\n",
    "    part def XtremeWheel :> Wheel;\n",
    "    \n",
    "    // Variability\n",
    "    variation part def EngineChoices :> BatmobileEngine {\n",
    "        variant part sEngine : StandardEngine;\n",
    "        variant part xEngine : XtremeEngine;\n",
    "    }\n",
    "    variation part def WheelChoices :> Wheel {\n",
    "        variant part sWheel : StandardWheel;\n",
    "        variant part xWheel : XtremeWheel;\n",
    "    }\n",
    "    \n",
    "    // Configuration\n",
    "    part def BatmobileConfigurations :> Batmobile {\n",
    "        part :>> batmobileEngine : EngineChoices;\n",
    "        part :>> wheels[4] : WheelChoices;\n",
    "\n",
    "        assert constraint {\n",
    "            (batmobileEngine == batmobileEngine::xEngine and \n",
    "             wheels == wheels.xWheel)\n",
    "        }\n",
    "    }\n",
    "    \n",
    "    // Member Product\n",
    "    part def XBatmobile :> BatmobileConfigurations {\n",
    "      part :>> batmobileEngine = batmobileEngine.xEngine;\n",
    "      part :>> wheels = wheels.xWheel;\n",
    "    }   \n",
    "}"
   ]
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "SysML",
   "language": "sysml",
   "name": "sysml"
  },
  "language_info": {
   "codemirror_mode": "sysml",
   "file_extension": ".sysml",
   "mimetype": "text/x-sysml",
   "name": "SysML",
   "pygments_lexer": "java",
   "version": "1.0.0"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 5
}
